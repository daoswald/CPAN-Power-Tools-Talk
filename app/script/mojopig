#!/usr/bin/env perl
use strict;
use warnings;

use Mojolicious::Lite;
use Lingua::PigLatin qw( piglatin );

any [ qw( GET POST ) ] => '/' => sub {
  my $self = shift;
  my $clear_text = $self->param('phrase');
  my $output = defined $clear_text ? piglatin($clear_text) : '';
  $self->render('index', piggy_phrase => $output );
};

app->start;

__DATA__

@@ index.html.ep
% layout 'default';
% title 'Pig Latin Translator';
<h3>Enter a plain language word or phrase for translation:</h3>
<p>
%= form_for '/' => (method => 'GET') => begin
    %= text_area phrase => 'Just another Perl hacker,'
    %= submit_button
% end
</p>
<h3>Piglatin!</h3>
<p><%= $piggy_phrase %></p>

@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
  <head><title><%= title %></title></head>
  <body><%= content %></body>
</html>
